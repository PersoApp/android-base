apply plugin: 'com.android.library'
apply plugin: 'maven'

version = '1.7.3'
group = 'net.vrallev.android.library'

dependencies {
    provided 'com.facebook.crypto:conceal:1.0.0'
    //noinspection GradleDependency
    provided 'com.android.support:support-v4:19.1.0'

    provided 'com.squareup.dagger:dagger:1.2.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 19
        versionCode 13
        versionName=version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

afterEvaluate { project ->
    uploadArchives {
        //noinspection GradleMisplacedStatement
        repositories {
            mavenDeployer {
                def mavenRepositoryUrl
                if (!project.hasProperty('mvnRepoUrl')) {
                    println "Settings for mvnRepoUrl are missing/incomplete for ${project.name}"
                    mavenRepositoryUrl = mavenLocal().getUrl()
                } else {
                    mavenRepositoryUrl = project.getProperties().get('mvnRepoUrl')
                }

                //mavenRepositoryUrl = "file:///${System.env.HOME}/.m2/repository"

                repository(url: mavenRepositoryUrl)

                pom.project {
                    name 'Android Base Library'
                    packaging 'aar'
                    description 'An collection of several helper classes and methods'
                    url 'https://github.com/vRallev/android-base'

                    scm {
                        url 'scm:git@github.com:vRallev/android-base.git'
                        connection 'scm:git@github.com:vRallev/android-base.git'
                        developerConnection 'scm:git@github.com:vRallev/android-base.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'vRallev'
                            name 'Ralf Wondratschek'
                            email 'r.wondr@gmail.com'
                        }
                    }
                }
            }
        }
    }

    //noinspection GroovyAssignabilityCheck
    task androidJavadocs(type: Javadoc) {
        failOnError = false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    //noinspection GroovyAssignabilityCheck
    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        //basename = artifact_id
        from androidJavadocs.destinationDir
    }

    //noinspection GroovyAssignabilityCheck
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        //basename = artifact_id
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        //archives packageReleaseJar
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
